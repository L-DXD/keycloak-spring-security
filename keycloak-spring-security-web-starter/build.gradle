import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
    id 'com.vanniktech.maven.publish'
    id 'signing'
}

version = webVersion  // web과 동일 버전
description = 'Keycloak Spring Security Web Starter - Auto-configuration for Servlet applications'

dependencies {
    // 자동 설정을 위한 필수 의존성
    api 'org.springframework.boot:spring-boot-autoconfigure'

    // 세션 관리를 위한 Spring Session Core 의존성
    api 'org.springframework.session:spring-session-core'

    // ConfigurationProperties 스캔 및 메타데이터 생성을 위한 애노테이션 프로세서
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Servlet 환경을 지원하기 위해 포함
    api project(':keycloak-spring-security-web')

    // 사용자의 프로젝트 환경에 따라 필요한 의존성만 전이되도록 compileOnly로 설정
    compileOnly 'org.springframework.boot:spring-boot-starter-web'

    // Redis Session Support (선택적 의존성)
    // 라이브러리 사용자가 Redis를 원할 때만 자신의 프로젝트에 추가하면 됨
    compileOnly 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'org.springframework.session:spring-session-data-redis'
}

signing {
    def secretFile = file("${rootDir}/secret.asc")
    def signingKey = secretFile.exists() ? secretFile.text : null

    if (signingKey != null) {
        useInMemoryPgpKeys(
                "-----BEG" + signingKey,
                findProperty("signing.password") as String
        )
        sign publishing.publications
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()
    coordinates(mavenGroupId, "keycloak-spring-security-web-starter", version as String)

    pom {
        name = "keycloak-spring-security-web-starter"
        description = project.description
        inceptionYear = "2025"
        url = mavenProjectUrl

        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/licenses/MIT"
                distribution = "repo"
            }
        }

        developers {
            developer {
                id = developerId
                name = developerName
                url = developerUrl
            }
        }

        scm {
            url = mavenProjectUrl
            connection = "scm:git:git://${mavenScmUrl}"
            developerConnection = "scm:git:ssh://git@${mavenScmUrl}"
        }
    }
}
