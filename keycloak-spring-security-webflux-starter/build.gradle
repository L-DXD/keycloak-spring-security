import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
    id 'com.vanniktech.maven.publish'
    id 'signing'
}

version = projectVersion
description = 'Keycloak Spring Security WebFlux Starter - Auto-configuration for Reactive applications'

dependencies {
    // 자동 설정을 위한 필수 의존성
    api 'org.springframework.boot:spring-boot-autoconfigure'

    // ConfigurationProperties 스캔 및 메타데이터 생성을 위한 애노테이션 프로세서
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Reactive 환경을 지원하기 위해 포함 (배포 시 자동으로 Maven 좌표로 변환됨)
    api project(':keycloak-spring-security-webflux')

    // 사용자의 프로젝트 환경에 따라 필요한 의존성만 전이되도록 compileOnly로 설정
    compileOnly 'org.springframework.boot:spring-boot-starter-webflux'
}

signing {
    def secretFile = file("${rootDir}/secret.asc")
    def signingPassword = findProperty("signingInMemoryKeyPassword")
        ?: findProperty("signing.password")

    if (secretFile.exists()) {
        def signingKey = secretFile.text
        useInMemoryPgpKeys(signingKey, signingPassword as String)
        sign publishing.publications
    } else {
        println "secret.asc 파일이 없습니다. GPG 서명을 건너뜁니다."
    }
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()

    coordinates(mavenGroupId, "keycloak-spring-security-webflux-starter", version as String)

    pom {
        name = "keycloak-spring-security-webflux-starter"
        description = project.description
        inceptionYear = "2025"
        url = mavenProjectUrl

        licenses {
            license {
                name = "MIT License"
                url = "https://opensource.org/licenses/MIT"
                distribution = "repo"
            }
        }

        developers {
            developer {
                id = developerId
                name = developerName
                url = developerUrl
            }
        }

        scm {
            url = mavenProjectUrl
            connection = "scm:git:git://${mavenScmUrl}"
            developerConnection = "scm:git:ssh://git@${mavenScmUrl}"
        }
    }
}
